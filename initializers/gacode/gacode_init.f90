PROGRAM gacode_init

  USE plasma_state_mod
  USE expro
  USE swim_global_data_mod, only : &
            & rspec, ispec, &               ! int: kind specification for real and integer
            & SWIM_name, SWIM_filename, &   ! derived data types: containing one character
                                            ! string
            & SWIM_error                    ! subroutine: a simple error handling routine

  IMPLICIT NONE

  !Internal data
  DOUBLE PRECISION :: &
       Zmin
  DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE :: &
       f0,f1,del,rho_zcntr
  INTEGER:: &
       ierr=0, status=0, iout=0, minid=0, fusid=0, &
       nrho, i, ii, j, nspecth_tmp, nspecrf_tmp, nspecfus_tmp
  INTEGER, DIMENSION(ps_max_static) :: & 
       amu_tmp, Zatom_tmp, Zion_tmp,amurf_tmp, Zatomrf_tmp, Zionrf_tmp, &
       amufus_tmp, Zatomfus_tmp, Zionfus_tmp
  LOGICAL:: &
       file_exists
  CHARACTER(LEN=*), PARAMETER :: &
       ps_init_nml_file = 'gacode_init.nml'
     
  !Namelists
  CHARACTER (len=256) :: cur_state_file, cur_eqdsk_file, cur_gacode_file, antspec_file
  INTEGER :: nicrf=1
  DOUBLE PRECISION :: icrfpwr=0.D0, minfrac =0.D0
  CHARACTER (len=256) :: addmin = 'None'
  LOGICAL :: antspec = .false.
  LOGICAL :: ministhermal = .false.
  namelist /ps_init_nml/ &
       cur_state_file, cur_eqdsk_file, cur_gacode_file, &
       icrfpwr, addmin, minfrac, ministhermal, antspec, antspec_file  

  ! Start of program logic
  ! -----------------------------------------------------------------------------
  WRITE(*,*) 'gacode_init'

  ! Read in the namelist generated by the python driver
  ! -----------------------------------------------------------------------------
  OPEN (unit=21, file = 'gacode_init.nml', status = 'old', &
        form = 'formatted', iostat = ierr)
  
  IF (ierr .ne. 0) THEN
	CALL SWIM_error ('open', 'generic_ps_init.f90',ps_init_nml_file)
	WRITE (*,*) 'generic_ps_init.f90: Cannot open ', TRIM(ps_init_nml_file)
	call exit(1)
  END IF

  READ(21, nml=ps_init_nml)
  CLOSE (21)
  WRITE (*, nml = ps_init_nml)

  
  ! Load GACODE file data
  ! -----------------------------------------------------------------------------
  CALL expro_read(trim(cur_gacode_file),0)
  
  ! Load equilibrium data from input_eqdsk_file if one is provided
  ! -----------------------------------------------------------------------------
  IF (TRIM(cur_eqdsk_file) /= ' ') THEN
        inquire(file=trim(cur_eqdsk_file), exist=file_exists)
        if(.not.file_exists)then
            write(*,*)'generic_ps_init : ERROR - input_eqdsk_file not found'
            write(*,*) trim(cur_eqdsk_file)
            status = 1
            call exit(status)
        endif
        write(*,*) 'generic_ps_init: input_eqdsk_file = ', trim(cur_eqdsk_file)
		CALL ps_update_equilibrium(ierr, TRIM(cur_eqdsk_file))
  END IF

  ! If antenna spectrum provided load its data
  ! ----------------------------------------------------------------------------
  
  
  ! Allocate plasma state based on GACODE values
  ! ----------------------------------------------------------------------------

  !allocate species into plasma state
  amu_tmp     = ps_unInit
  Zatom_tmp   = ps_unInit
  Zion_tmp    = ps_unInit
  amurf_tmp   = ps_unInit
  Zatomrf_tmp = ps_unInit
  Zionrf_tmp  = ps_unInit
  amufus_tmp   = ps_unInit
  Zatomfus_tmp = ps_unInit
  Zionfus_tmp  = ps_unInit
  
  !determine number of fast ions in gacode input file. use this to set
  !array dimensions and ion parameters
  nspecth_tmp = 0
  nspecrf_tmp = 0
  nspecfus_tmp = 0
  j=0
  do i=1,expro_n_ion
     !find the rf minority species (if there is one)
     if ((expro_type(i).eq.'[fast]').and.(expro_name(i).ne.'He')) then
        nspecrf_tmp = 1
        minid = i
        amurf_tmp(1)    = nint(expro_mass(i))
        Zatomrf_tmp(1)  = nint(expro_z(i))
        Zionrf_tmp(1)   = nint(expro_z(i))
     !if fast helium ash set as fusion product
     elseif ((expro_type(i).eq.'[fast]').and.(expro_name(i).eq.'He'))then
        nspecfus_tmp = 1
        fusid = i
        amufus_tmp(1)    = nint(expro_mass(i))
        Zatomfus_tmp(1)  = nint(expro_z(i))
        Zionfus_tmp(1)   = nint(expro_z(i))
     elseif (((expro_name(i).eq.'He3').or.(expro_name(i).eq.'H')) &
        .and.(ministhermal))then
        nspecrf_tmp = 1
        minid = i
        amurf_tmp(1)    = nint(expro_mass(i))
        Zatomrf_tmp(1)  = nint(expro_z(i))
        Zionrf_tmp(1)   = nint(expro_z(i))
     !set others as thermal   
     else 
        nspecth_tmp = nspecth_tmp+1
        j = j+1
        amu_tmp(j)    = nint(expro_mass(i))
        Zion_tmp(j)   = nint(expro_z(i))
        !fixes issue w/ assuming zion.eq.zatom that causes namrd to die
        if(expro_name(i).eq.'W')then
           Zatom_tmp(j)  = 74
        else
           Zatom_tmp(j)  = nint(expro_z(i))
        endif
     endif
  enddo

  if (trim(addmin).ne.'None') then
     nspecrf_tmp = 1
     minid = expro_n_ion+1
     if (trim(addmin).eq.'He3') then
        amurf_tmp(1) = 3
        Zatomrf_tmp(1) = 2
        Zionrf_tmp(1) = 2
     elseif (trim(addmin) .eq. 'H') then
        amurf_tmp(1) = 1
        Zatomrf_tmp(1) = 1
        Zionrf_tmp(1) = 1
     endif
  endif   
  
  !Set species properties and allocate into the plasma state
  CALL ps_namrd_slist_chk("S", ps_max_static, amu_tmp, &
       Zatom_tmp, Zion_tmp, ps%nspec_th, iout, ierr) 
  WRITE(*,*) 'gacode_init: Thermal species read in. ierr=', ierr
  IF(IERR.NE.0) RETURN

  CALL ps_namrd_slist_chk("SFUS", ps_max_static, amufus_tmp,  &
       Zatomfus_tmp, Zionfus_tmp, ps%nspec_fusion, iout, ierr) 
  WRITE(*,*) 'gacode_init: Fus species read in. ierr=', ierr
  IF(IERR.NE.0) RETURN

  CALL ps_namrd_slist_chk("RFMIN", ps_max_static, amurf_tmp,  &
       Zatomrf_tmp, Zionrf_tmp, ps%nspec_rfmin, iout, ierr) 
  WRITE(*,*) 'gacode_init: RF species read in. ierr=', ierr
  IF(IERR.NE.0) RETURN

  !allocate ion cyclotron source in plasma state
  ps%nicrf_src = 1 !always use one source
  if(antspec)then
     !put antenna spectrum into plasma state for later use
  else
     ps%max_n_straps = 0
     ps%max_num_nphi_vac = 1
     ps%max_num_nphi = 1
  endif
  
  CALL plasma_state_empty_alloc(ps,ierr)
  WRITE(*,*) 'gacode_init: Plasma state empty alloc. ierr =', ierr
  IF(IERR.NE.0) RETURN

  IF(ALLOCATED(ps%qatom_S)) THEN
     CALL ps_species_convert(-1, -1, 0, & 
          ps%qatom_S(0), ps%q_S(0), ps%m_S(0), ierr)
     IF(ierr.NE.0) RETURN
 
     DO II = 1,ps%NSPEC_TH
        CALL ps_species_convert(Zatom_tmp(ii),Zion_tmp(ii),AMU_tmp(ii), &
             ps%qatom_S(ii), ps%q_S(ii), ps%m_S(ii), ierr)
        WRITE(*,*) 'gacode_init: Plasma state set th species ierr =', ierr
        IF(ierr.NE.0) RETURN
     ENDDO
  ENDIF
 
  IF(ALLOCATED(ps%qatom_SFUS)) THEN
     DO II = 1,ps%NSPEC_FUSION
        CALL ps_species_convert(ZatomFUS_tmp(ii),ZionFUS_tmp(ii),AMUFUS_tmp(ii), &
             ps%qatom_SFUS(ii), ps%q_SFUS(ii), ps%m_SFUS(ii), ierr)
        WRITE(*,*) 'gacode_init: Plasma state set fus species ierr =', ierr
        IF(ierr.NE.0) RETURN
     ENDDO
  ENDIF 
  
  IF(ALLOCATED(ps%qatom_RFMIN)) THEN
     DO II = 1,ps%NSPEC_RFMIN
        CALL ps_species_convert(ZatomRF_tmp(ii),ZionRF_tmp(ii),AMURF_tmp(ii), &
             ps%qatom_RFMIN(ii), ps%q_RFMIN(ii), ps%m_RFMIN(ii), ierr)
        WRITE(*,*) 'gacode_init: Plasma state set RF species ierr =', ierr
        IF(ierr.NE.0) RETURN
     ENDDO
  ENDIF
  
  CALL ps_label_species(ierr)
    IF(ierr.NE.0) THEN
       WRITE(iout,*) ' ?ps_sconfig_read: ps_label_species error.'
       RETURN
    ENDIF
    
  CALL ps_merge_species_lists(ierr)
  IF(ierr.NE.0) THEN
     WRITE(iout,*) ' ?ps_sconfig_read: ps_merge_species_lists error.'
     RETURN
  ENDIF

  !allocate rho array size
  ps%nrho = expro_n_exp
  if(ps%nspec_rfmin.gt.0) ps%nrho_icrf = ps%nrho
  if(ps%nspec_fusion.gt.0) ps%nrho_fus = ps%nrho
  
  !allocate arrays
  CALL ps_alloc_plasma_state(ierr)
  WRITE(*,*) 'gacode_init: Thermal profile arrays allocated. ierr =', ierr
  IF(IERR.NE.0) RETURN

  ! Copy GACODE data into the plasma state
  ! ----------------------------------------------------------------------------
  
  !copy gacode rho grid
  ps%rho = expro_rho
  if(ps%nspec_rfmin.gt.0) ps%rho_icrf = ps%rho
  if(ps%nspec_fusion.gt.0) ps%rho_fus = ps%rho
     
  !set up interpolation grid
  !profs want zone centered quantities... because reasons
  allocate(rho_zcntr(ps%nrho-1))
  do i=1,ps%nrho-1
     rho_zcntr(i) = 0.5*(ps%rho(i)+ps%rho(i+1))
  enddo
  
  !copy gacode T/n profiles
  !electrons
  call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_te,ps%Ts(:,0),ierr)

  !little bit of complication here if we want to set the RF
  !minority density as a fraction of the electron density. the code will
  !default to using the value passed by gacode, but if namelist value is
  !set this will be overwritten. the reset here is /not/ fully
  !self consistent as i don't reset the Zeff used in the plasma state
  !but good enough for scoping optimal minority density
  if (minfrac.gt.0.D0)then
     allocate(f0(ps%nrho))
     allocate(f1(ps%nrho))
     allocate(del(ps%nrho))
     if (trim(addmin).ne.'None')then
        f0 = 0.0
        Zmin = Zionrf_tmp(1) 
     else
        f0 = expro_ni(minid,:)/expro_ne(:) !initial minfract
        Zmin = expro_z(minid)
     endif
     f1(:) = minfrac                    !desired minfract
     
     del = (f1 - f0)/(1.D0/Zmin - f1)
     expro_ne = expro_ne + expro_ne * del
     deallocate(f0,f1,del)
     ps%kdens_rfmin = "fraction"
     ps%fracmin(1) = minfrac
  else
     ps%kdens_rfmin = "data"
  endif
  call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_ne*1.D19,ps%ns(:,0),ierr)
  ps%isThermal(1) = 1
  
  !ions
  j=1
  do i=1,expro_n_ion
     if ((i.ne.minid).and.(i.ne.fusid)) then
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_ni(i,:)*1.D19,ps%ns(:,j),ierr)
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(i,:),ps%Ts(:,j),ierr)
        j = j+1 
     endif
  enddo

  if (fusid.ne.0) then
     call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_ni(fusid,:)*1.D19,ps%nfusi(:,1),ierr)
     call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(fusid,:),ps%eperp_fusi(:,1),ierr)
     call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(fusid,:),ps%epll_fusi(:,1),ierr)
  endif

  !this will write the minority energies from the GACODE file, but you can ignore them
  !they aren't going to do anything in the TORIC wrappers as is
  if (minid.ne.0) then
     if (trim(addmin).ne.'None')then
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,minfrac*expro_ne*1.D19,ps%nmini(:,0),ierr)
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(1,:),ps%eperp_mini(:,1),ierr)
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(1,:),ps%epll_mini(:,1),ierr)
     else 
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_ni(minid,:)*1.D19,ps%nmini(:,1),ierr)
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(minid,:),ps%eperp_mini(:,1),ierr)
        call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Ti(minid,:),ps%epll_mini(:,1),ierr)
     endif
  endif

  !copy other quantities of interest into plasma state
  call ps_user_1dintrp_vec(rho_zcntr,expro_rho,expro_Z_eff,ps%Zeff(:),ierr)
  ps%v_loop(:) = 0.0 !don't want this mucking up CQL3D simulations rn
  ps%shot_number = expro_shot

  ! Copy antenna spectrum data plasma state
  ! -----------------------------------------------------------------------------
  if(antspec)then
     !put antenna spectrum into plasma state for later use
  else
     ps%num_nphi(1) = 1
     ps%wt_nphi(1,1) = 1.0_rspec
  endif

  !put some ICRF things into ps
  ps%icrf_src_name(1)= 'IC1'
  ps%power_ic(1) = icrfpwr
  
  ! Store plasma state
  ! -----------------------------------------------------------------------------
  CALL PS_STORE_PLASMA_STATE(ierr, trim(cur_state_file))
  WRITE(*,*) "gacode_init: stored initial plasma state"

  deallocate(rho_zcntr)

  
CONTAINS
  
END PROGRAM gacode_init
